apply plugin: 'com.android.application'
apply plugin: 'AndResGuard'
apply plugin: 'io.fabric'

buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }
    dependencies {
        // The Fabric Gradle plugin uses an open ended version to react
        // quickly to Android tooling updates
        classpath 'io.fabric.tools:gradle:latest.integration'
    }
}

repositories {
    jcenter()
    maven { url 'https://maven.fabric.io/public' }
}

crashlytics {
    enableNdk true
    androidNdkOut 'src/main/obj'
    androidNdkLibsOut 'src/main/libs'
}


android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"


    signingConfigs {
        release {
            try {
                storeFile file("../keystore/release.keystore")
                storePassword "testres"
                keyAlias "testres"
                keyPassword "testres"
            } catch (ex) {
                throw new InvalidUserDataException(ex.toString())
            }
        }

        debug {
            storeFile file("../keystore/debug.keystore")
        }
    }

    productFlavors {
        flavor2 {
            println("@@@THIS IS FLAVOR2@@@")
            applicationId 'andresguard.tencent.com.andresguard_example.flavor2'
        }

        flavor1 {
            println("@@@THIS IS FLAVOR1@@@")
            applicationId 'andresguard.tencent.com.andresguard_example.flavor1'
        }

        aaa {
            println("@@@THIS IS aaa@@@")
            applicationId 'andresguard.tencent.com.andresguard_example.aaa'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            shrinkResources true
            zipAlignEnabled true
            pseudoLocalesEnabled true
            signingConfig signingConfigs.release
        }
    }


    defaultConfig {
        applicationId "andresguard.tencent.com.andresguard_example"
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
}

andResGuard {
    mappingFile = null
    use7zip = false
    useSign = true
    keepRoot = false
    // add <yourpackagename>.R.drawable.icon into whitelist.
    // because the launcher will get the icon with his name
    whiteList = [
            "andresguard.tencent.com.andresguard_example.R.drawable.icon",
            "andresguard.tencent.com.andresguard_example.flavor2.R.drawable.icon",
            "andresguard.tencent.com.andresguard_example.flavor1.R.drawable.icon",
            "andresguard.tencent.com.andresguard_example.aaa.R.drawable.icon",
            //https://docs.fabric.io/android/crashlytics/build-tools.html
            "andresguard.tencent.com.andresguard_example.R.string.com.crashlytics.*",
            "andresguard.tencent.com.andresguard_example.flavor2.R.string.com.crashlytics.*",
            "andresguard.tencent.com.andresguard_example.flavor1.R.string.com.crashlytics.*",
            "andresguard.tencent.com.andresguard_example.aaa.R.string.com.crashlytics.*"
    ]
    compressFilePattern = [
            "*.png",
            "*.jpg",
            "*.jpeg",
            "*.gif",
            "resources.arsc"
    ]
    //FIXME:change to your zipalign path
    zipAlignPath = '/Users/sun/Library/Android/sdk/build-tools/21.1.1/zipalign'

    //FIXME:change to your 7za path
    sevenZipPath = '/usr/local/bin/7za'
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.0'
    compile 'com.android.support:design:23.2.0'
    // Crashlytics Kit
    compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
        transitive = true
    }
    // NDK Kit
    compile('com.crashlytics.sdk.android:crashlytics-ndk:1.1.2@aar') {
        transitive = true
    }
}
